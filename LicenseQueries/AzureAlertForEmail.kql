let endTime = now();
let startTime = endTime - 24h;
AuditLogs
| where TimeGenerated between (startTime .. endTime)
| where Result == "success"
| where OperationName has_any("Remove member from group", "Add member to group")
| where Category has_any("GroupManagement")
| extend InitiatedByUser = todynamic(InitiatedBy)
| extend UserDisplayName = InitiatedByUser.user.displayName,
         UserId = InitiatedByUser.user.id,
         UserPrincipalName = InitiatedByUser.user.userPrincipalName,
         UserIpAddress = InitiatedByUser.user.ipAddress,
         TargetResourcesJson = todynamic(TargetResources)
| mv-expand TargetResource = TargetResourcesJson
| extend TargetResourceId = TargetResource.id,
         TargetResourceDisplayName = TargetResource.displayName,
         TargetResourceType = TargetResource.type,
         TargetResourceUserPrincipalName = TargetResource.userPrincipalName,
         ModifiedProperties = TargetResource.modifiedProperties
| mv-expand ModifiedProperty = ModifiedProperties
| extend ModifiedPropertyDisplayName = ModifiedProperty.displayName,
         ModifiedPropertyOldValue = ModifiedProperty.oldValue,
         ModifiedPropertyNewValue = ModifiedProperty.newValue
| project TimeGenerated, OperationName, AADOperationType, UserId, UserPrincipalName, UserIpAddress, TargetResourceId, TargetResourceDisplayName, TargetResourceType, TargetResourceUserPrincipalName, ModifiedPropertyDisplayName, ModifiedPropertyOldValue, ModifiedPropertyNewValue
